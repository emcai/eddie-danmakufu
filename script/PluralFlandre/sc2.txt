#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Flandre Scarlet SC 2"]

#include "shot/AllStarShot_Const.txt"
#include "script/helper.txt"
#include "script/cutin.txt"
#include "script/boss_lib/GizmoSpriteLibrary.txt"
#include "script/boss_lib/GizmoFSpriteLibrary.txt"

let objBoss;
let objFairy;
let objScene = GetEnemyBossSceneObjectID();
let frame = -80;
let followBullet;
let objBullet; let objBulletWave;

@Event
{
	alternative(GetEventType())
	case (EV_REQUEST_LIFE) {
		SetScriptResult(600);
	}
	case (EV_REQUEST_TIMER) {
		SetScriptResult(50);
	}
	case (EV_PLAYER_REBIRTH) {
		deleteAll;
	}
}

@Initialize
{
	InstallFont("font/lucon.ttf");
	objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
	ObjEnemy_Regist(objBoss);
	ObjEnemy_SetDamageRate(objBoss, 20, 4);
	
	objFairy = ObjEnemy_Create(OBJ_ENEMY);
	ObjEnemy_Regist(objFairy);
	ObjEnemy_SetLife(objFairy, 999999999);
	
	let cutin = "img/Flandre Scarlet/cutin.png";
	ObjCutin_LaunchS3(NAZRIN, cutin, "Lunatic");
	let card1 = ObjText_Create();
	ObjText_SetText(card1, "Fear \"No Escape from the Past\"");
	ObjText_SetFontSize(card1, 16);
    ObjText_SetFontType(card1, "Ludica Console");
    ObjText_SetFontBold(card1, true);
	ObjRender_SetX(card1, 80);
	ObjRender_SetY(card1, 15);
	
	ObjEnemyBossScene_StartSpell(objScene);

	renderSanaeMoF(objBoss);
	ObjSprite2D_SetSourceRect(objBoss, 10, 10, 40, 40); 
	ObjSprite2D_SetDestCenter(objBoss); 

	let cx = GetStgFrameWidth() / 2;
	ObjMove_SetDestAtFrame(objBoss, cx, 85, 20);
	
	objBullet = createSoundEffect("script/snd/Bullet.wav");
	objBulletWave = createSoundEffect("script/snd/BulletWave.wav");
}

@MainLoop
{
	let ex = ObjMove_GetX(objBoss);
	let ey = ObjMove_GetY(objBoss);
	
	ObjEnemy_SetIntersectionCircleToShot(objBoss, ex, ey, 32);
	ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ex, ey, 24);
	
	frame++;
	
	if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0)
	{
		TFinalize;
	}
	
	if (frame == 0){
		startFire;
	}
	
	if (frame % 60 == 0 && frame > 0){
		if (GetPlayerState() != STATE_DOWN) {
			follow;
			fire;
		}
		else {
			ObjMove_SetDestAtFrame(objFairy, GetStgFrameWidth() / 2, 85, 10);
		}
	}
	
	if (frame % 30 == 0 && frame >= 60){
		fire2;
	}	
	
	yield;
}

task TFinalize {
	if(ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT)
      +ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT) == 0){
		AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
	}
	
	Obj_Delete(objBoss);
	DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
	SetAutoDeleteObject(true);
	CloseScript(GetOwnScriptID());
	return;
}

//fairy to follow the character
task startFire {
	renderTinyGoreF(objFairy, 1);
	ObjShot_SetSpellResist(objFairy, true);
	ObjMove_SetDestAtFrame(objFairy, GetStgFrameWidth() / 2, 85, 10);
}

//let the fairy follow the player
task follow {
	ObjShot_SetSpellResist(objFairy, true);
	ObjMove_AddPatternA1(objFairy, 0, 3, getAngleTo(objFairy, GetPlayerObjectID()));
	wait(30);
	ObjMove_AddPatternA1(objFairy, 0, 0, 0);
}

//put other bullets in path of fairy
task fire {
	wait(10);
	ObjSound_Play(objBulletWave);
	let one = CreateShotOA1(objFairy, 0, 0, SMALL_RED, 0);
	wait(10);
	ObjSound_Play(objBulletWave);
	let two = CreateShotOA1(objFairy, 0, 0, SMALL_RED, 0);
	wait(100);
	
	let loopCount = 12;
	let temp = 0;
	let buff = randRange(360 / loopCount);
	ObjSound_Play(objBullet);
	while (temp < loopCount){
		CreateShotOA1(one, 2.5, buff + temp * (360 / loopCount), DOT_BLUE, 0);
		CreateShotOA1(two, 2.5, buff + temp * (360 / loopCount), DOT_BLUE, 0);
		CreateShotOA1(one, 2, buff + temp * (360 / loopCount), DOT_BLUE, 0);
		CreateShotOA1(two, 2, buff + temp * (360 / loopCount), DOT_BLUE, 0);
		temp += 1;
	}
	Obj_Delete(one);
	Obj_Delete(two);
}

//boss shoots bullets at the player
task fire2 {
	let ex = ObjMove_GetX(objBoss);
	let ey = ObjMove_GetY(objBoss);
	let loopCount = 6;
	let temp = 0;
	while (temp < loopCount){
		CreateShotA1(ex, ey, 2, (frame / 10) + 0 + (temp * (180 / loopCount)), DOT_RED, 10);
		CreateShotA1(ex, ey, 2, (frame / 10) + 180 + (temp * (180 / loopCount)), DOT_RED, 10);
		temp += 1;
	}
}